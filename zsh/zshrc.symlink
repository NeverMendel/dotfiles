# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our sh files
typeset -U config_files
config_files=($ZSH/**/*.sh)

# load the path files
for file in ${(M)config_files:#*/path.sh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.sh}:#*/completion.sh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.sh}
do
  source $file
done

unset config_files

bindkey '^[[A' history-substring-search-up
bindkey '^[[B' history-substring-search-down

source $(brew --prefix)/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
source $(brew --prefix)/share/zsh-autosuggestions/zsh-autosuggestions.zsh
source $(brew --prefix)/share/zsh-history-substring-search/zsh-history-substring-search.zsh
# source $(brew --prefix)/share/powerlevel10k/powerlevel10k.zsh-theme

# plugins=(
#   git
#   zsh-autosuggestions
#   zsh-syntax-highlighting
# )

# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
# [[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh
